NB. {:y -----------------------------------------------------------------

tail =. _1&{

4         -: {: 4
4         -: {: i.5
'.'       -: {: 'Cogito, ergo sum.'
(<'sum.') -: {: ;:'Cogito, ergo sum.'
8 9 10 11 -: {: i.3 4

NB. Boolean
(tail -: {:) 1=?2
(tail -: {:) 1=?4$2
(tail -: {:) 1=?3 4$2
(tail -: {:) 1=?2 3 4$2

NB. literal
(tail -: {:) 'a'
(tail -: {:) a.{~?4$256
(tail -: {:) a.{~?3 4$256
(tail -: {:) a.{~?2 3 4$256

NB. integer
(tail -: {:) 12345
(tail -: {:) ?4$123456
(tail -: {:) ?3 4$123456
(tail -: {:) ?2 3 4$123456

NB. floating point
(tail -: {:) 123.45
(tail -: {:) o.?4$1236
(tail -: {:) o.?3 4$1256
(tail -: {:) o.?2 3 4$1456

NB. complex
(tail -: {:) 123j45
(tail -: {:) ^0j1*?4$1236
(tail -: {:) ^0j1*?3 4$1256
(tail -: {:) ^0j1*?2 3 4$1456

NB. boxed
t=.(+&.>i.5),;:'(tail -: {:) ^0j1*?3 4$1256'
(tail -: {:) <123j45
(tail -: {:) t{~?4$#t
(tail -: {:) t{~?3 4$#t
(tail -: {:) t{~?2 3 4$#t

(  3$0  ) -: {: 0   3$1
(2 3$' ') -: {: 0 2 3$'a'
(  3$0  ) -: {: 0   3$12345
(  3$0  ) -: {: 0   3$123.45
(  3$0  ) -: {: 0   3$123j45
(1 3$a: ) -: {: 0 1 3$<45

(2 3$3j4) -: {:!.3j4 i. 0 2 3


4!:55 ;:'t tail'


